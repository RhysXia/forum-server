buildscript {
    ext {
        springBootVersion = '1.5.12.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'

    group 'site.rhys.forum'
    version '1.0-SNAPSHOT'
    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
    }

    ext {
        springCloudVersion = 'Edgware.SR3'
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    if (project.name.endsWith('impl')) {
        dependencies {
            testCompile('org.springframework.boot:spring-boot-starter-test')
            compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
        }
    }


}

project(':forum-center') {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-server')
    }
}

project(':forum-gateway') {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-starter-zuul')
    }
}

project(':forum-config') {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-config-server')
    }
}