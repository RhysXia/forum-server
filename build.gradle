buildscript {
    ext {
        springBootVersion = '1.5.12.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'

    group 'site.rhys.forum'
    version '1.0-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        springCloudVersion = 'Edgware.SR3'
        lombokVersion = '1.16.10'
        springBootMybatisVersion = '1.3.2'
        springBootDruidVersion = '1.1.9'
        springBootMybatisPageHaplerVersion = '1.2.3'
        swaggerVersion = '2.8.0'
    }

    repositories {
        mavenCentral()
    }


    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }


}


project(':forum-config') {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-config-server')
    }
}

project(':forum-center') {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-starter-config')
        compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-server')
    }
}

project(':forum-gateway') {
    dependencies {
        compile('org.springframework.cloud:spring-cloud-starter-config')
        compile('org.springframework.cloud:spring-cloud-starter-zuul')
        compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
    }
}

project(':forum-common') {
    dependencies {
        compileOnly("org.projectlombok:lombok:${lombokVersion}")
    }
}


subprojects {
    if (project.name.startsWith('forum-service')) {
        dependencies {
            compile("org.springframework.boot:spring-boot-devtools")
            compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')
            compile('org.springframework.cloud:spring-cloud-starter-config')
            compile('org.springframework.cloud:spring-cloud-starter-hystrix')
            compile("com.alibaba:druid-spring-boot-starter:${springBootDruidVersion}")
            compile("io.springfox:springfox-swagger2:${swaggerVersion}")
            compile("io.springfox:springfox-swagger-ui:${swaggerVersion}")
            compile("org.mybatis.spring.boot:mybatis-spring-boot-starter:${springBootMybatisVersion}")
            compile("com.github.pagehelper:pagehelper-spring-boot-starter:${springBootMybatisPageHaplerVersion}")
            compile(project(':forum-common'))
            runtime('mysql:mysql-connector-java')
            compileOnly("org.projectlombok:lombok:${lombokVersion}")
            testCompile('org.springframework.boot:spring-boot-starter-test')
        }
    }
}